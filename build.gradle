buildscript {
    ext {
        springBootVersion = '2.0.0.RELEASE'
        dependencyCheckVersion = '3.1.2'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.owasp:dependency-check-gradle:${dependencyCheckVersion}")
    }
}

plugins {
    id "nebula.ospackage" version "4.5.1"
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'
apply plugin: 'org.owasp.dependencycheck'

group = 'org.bahmni.mart'
version = '2.0.6'

description = "Batch job for Bahmni Mart"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

jacoco {
    toolVersion = "0.7.9"
}

jacocoTestReport {
    dependsOn "test"
}

test {
    systemProperties System.properties
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

jacocoTestCoverageVerification {
    dependsOn "jacocoTestReport"
    violationRules {
        rule {
            limit {
                minimum = 0.7
            }
        }

        rule {
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.7
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

dependencies {
    compile('org.springframework.boot:spring-boot-starter')
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.13'
    compile group: 'ch.qos.logback', name: 'logback-core', version: '1.2.13'
    compile group: 'org.yaml', name: 'snakeyaml', version: '2.0'
    compile("org.springframework.boot:spring-boot-starter-batch")
    compile group: 'org.springframework', name: 'spring-beans', version: '5.3.20'
    compile group: 'org.springframework', name: 'spring-core', version: '5.3.27'
    compile group: 'org.springframework', name: 'spring-expression', version: '5.3.27'
    compile group: 'org.codehaus.jettison', name: 'jettison', version: '1.5.4'
    compile group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.20'
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.21.7'
    compile("org.springframework.batch:spring-batch-test")
    compile group: 'junit', name: 'junit', version: '4.13.1'
    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.42'
    compile group: 'org.postgresql', name: 'postgresql', version: '42.3.9'
    compile group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.3.0'
    compile group: 'commons-io', name: 'commons-io', version: '2.7'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
    compile group: 'org.freemarker', name: 'freemarker', version: '2.3.23'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.9'
    compile "com.github.jsqlparser:jsqlparser:1.1"
    compile 'org.springframework.cloud:spring-cloud-starter-task:1.2.2.RELEASE'
    compile group: 'org.liquibase', name: 'liquibase-core', version: '4.8.0'
    compile group: 'org.json', name: 'json', version: '20231013'

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile group: 'junit', name: 'junit', version: '4.13.1'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
    testCompile group: 'org.powermock', name: 'powermock-core', version: '1.6.4'
    testCompile "org.powermock:powermock-module-junit4:1.6.4"
    testCompile "org.powermock:powermock-module-junit4-rule:1.6.4"
    testCompile "org.powermock:powermock-api-mockito:1.6.4"
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
}

jar {
    manifest {
        attributes 'Main-Class': 'org.bahmni.mart.Application'
    }
}

ospackage {
    packageName = 'bahmni-mart'
    release = System.getenv('GO_PIPELINE_COUNTER') ?: 1
    arch = NOARCH
    os = LINUX
    user = 'root'

    into '/opt/bahmni-mart'

    from("${projectDir}/scripts/builds/rpm/") {
        fileMode = 0755
        createDirectoryEntry = true
        into '/opt/bahmni-mart/bin'
    }

    from("${buildDir}/libs") {
        include(String.format("%s-%s.jar", project.name, project.version))
        fileMode = 0755
        createDirectoryEntry = true
        rename(String.format("%s-%s.jar", project.name, project.version), String.format("%s.jar", project.name))
        into "/opt/bahmni-mart/lib"
    }

    from("${projectDir}/conf/bahmni-mart.json") {
        fileMode = 0755
        createDirectoryEntry = true
        into "/opt/bahmni-mart/conf"
    }

    from("${projectDir}/conf/liquibase.xml") {
        fileMode = 0766
        createDirectoryEntry = true
        into "/opt/bahmni-mart/conf"
    }

    from("${projectDir}/conf/bahmni-mart-scdf-liquibase.xml") {
        fileMode = 0766
        createDirectoryEntry = true
        into "/opt/bahmni-mart/conf"
    }
}

buildRpm {
    dependsOn "build", "jar"

    requires("cronie")


    postInstall file("${projectDir}/scripts/builds/postinstall.sh")
    postUninstall file("${projectDir}/scripts/builds/postuninstall.sh")
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.5.1'
}

checkstyle {
    project.ext.checkstyleVersion = '8.8'
    project.ext.sevntuChecksVersion = '1.27.0'
    ignoreFailures = false
    configFile = file("src/main/resources/checkStyle.xml")

    checkstyleMain {
        source = sourceSets.main.allSource
    }

    configurations {
        checkstyle
    }

    dependencies{
        assert project.hasProperty("checkstyleVersion")

        checkstyle "com.puppycrawl.tools:checkstyle:${checkstyleVersion}"
        checkstyle "com.github.sevntu-checkstyle:sevntu-checks:${sevntuChecksVersion}"
    }
}
